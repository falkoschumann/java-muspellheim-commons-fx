import com.github.spotbugs.SpotBugsTask

import java.nio.file.Files
import java.nio.file.Paths

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.openjfx:javafx-plugin:0.0.8"
    }
}

plugins {
    id 'java-library'
    id 'checkstyle'
    id 'jacoco'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.github.spotbugs' version '2.0.0'
    id 'io.freefair.lombok' version '4.1.2'
    id 'org.sonarqube' version '2.8'
}

if (!System.getProperty("java.version").startsWith("1.8")) {
    apply plugin: 'org.openjfx.javafxplugin'

    javafx {
        version = "11"
        modules = ['javafx.controls', 'javafx.fxml', 'javafx.graphics', 'javafx.swing']
    }
}

version = '4.0.0'

// ###################################################################
// Java Library
// ###################################################################

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
    jcenter()
}

java {
    registerFeature("junitSupport") {
        usingSourceSet(sourceSets.main)
    }
}

dependencies {
    junitSupportImplementation 'org.junit.jupiter:junit-jupiter:5.5.2'

    implementation 'de.muspellheim:muspellheim-commons:1.8.0'
    implementation 'org.controlsfx:controlsfx:8.40.15'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.2'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Muspellheim Commons FX'
        attributes 'Implementation-Version': project.version
        attributes 'Implementation-Vendor': 'Falko Schumann'
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
        exceptionFormat = 'full'
    }
}

javadoc {
    title = "Muspellheim Commons FX v${project.version}"
    options.links += 'https://docs.oracle.com/en/java/javase/11/docs/api/'
    options.links += 'https://falkoschumann.github.io/java-muspellheim-commons/'
    options.overview = file('src/main/javadoc/overview.html')
    options.noTimestamp = true
    destinationDir = file('docs')
}
build.dependsOn += javadoc

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// ###################################################################
// Checkstyle
// ###################################################################

checkstyle {
    toolVersion '8.24'
}

// ###################################################################
// Jacoco
// ###################################################################

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                value = 'COVEREDRATIO'
                minimum = 0.5
            }
        }
        rule {
            element = 'METHOD'
            limit {
                counter = 'COMPLEXITY'
                value = 'TOTALCOUNT'
                maximum = 10
            }
        }
    }
}
check.dependsOn += [jacocoTestCoverageVerification, jacocoTestReport]

// ###################################################################
// Bintray
// ###################################################################

ext {
    if (!project.hasProperty('bintrayUser')) {
        bintrayUser = 'bintray_user'
    }
    if (!project.hasProperty('bintrayKey')) {
        bintrayKey = 'bintray_key'
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['MuspellheimCommonsFXPublication']
    dryRun = false
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = bintrayUser
        licenses = ['MIT']
        vcsUrl = "https://github.com/falkoschumann/java-${project.name}.git"
        githubRepo = "falkoschumann/java-${project.name}"
        version {
            name = project.version
            desc = 'Bundles common classes for developing Java apps with JavaFX.'
            released = new Date()
            vcsTag = "v${project.version}"
        }
    }
}

// ###################################################################
// Maven
// ###################################################################

publishing {
    publications {
        MuspellheimCommonsFXPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'de.muspellheim'
            artifactId project.name
            version project.version
        }
    }
}

// ###################################################################
// SpotBugs
// ###################################################################

spotbugs {
    excludeFilter = file("$rootDir/config/spotbugs/excludeFilter.xml")
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = false
        text.enabled = true
    }
}

task printSpotBugsReportMain() {
    doLast {
        printSpotBugsReport('main')
    }
}
spotbugsMain.finalizedBy printSpotBugsReportMain

task printSpotBugsReportTest() {
    doLast {
        printSpotBugsReport('test')
    }
}
spotbugsTest.finalizedBy printSpotBugsReportTest

static def printSpotBugsReport(String sourceSet) {
    def report = Paths.get("build/reports/spotbugs/${sourceSet}.text")
    if (Files.exists(report)) {
        def lines = Files.readAllLines(report)
        lines.forEach { l -> println(l) }
    }
}

// ###################################################################
// Lombok
// ###################################################################

lombok {
    config['lombok.nonNull.exceptionType'] = 'Jdk'
    config['lombok.anyConstructor.addConstructorProperties'] = 'true'
}

// ###################################################################
// Sonarqube
// ###################################################################

sonarqube {
    properties {
        property "sonar.projectKey", "falkoschumann_java-muspellheim-commons-fx"
    }
}
